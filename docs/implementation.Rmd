---
title: "Implementation"
output:
  html_document:
    toc: true
    toc_float: true
    toc_depth: 3
---

## Procedure in LPX as an example

Fortran code used within LPX to read and interpret the land use forcing is given in the file `read_landuse_map.F`. The following steps (code extracted fromt that file) are crucial:

1. Define the gridcell area fraction that remains inaccessible and therefore primary land (not affected by land conversion), based on the file `land_suit_sage_lpjgr.nc`:
```Fortran
inaccess(jpngr)   = max( 0.d0, land_fraction(jpngr) - fsuit )
```

2. Define wether land is under permanent of non-permanent (shifting cultivation-type) agriculture, based on the file `perm_lpjgr_holoLU2.nc`. Under non-permanent agriculture, a constant land turnover rate of 0.25 is assumed (a quarter of cropland area abandoned each year, variable `ltor`), and the fraction of land suitable for cropland cultivation (`cropsuit`) is set to be equal to the (temporally constant) accessible area fraction. Under permanent agriculture, a fallow rotation regime is assumed, corresponding to a three-field rotation for all years before 1850 CE, gradually shifting to no fallow for all years after 1960 CE. This is implemented using the variable `fallow_factor`. In this case (pre-1850), the fraction of land suitable (used) for cropland cultivation is set to 1.5 times (`3.0d0/2.0d0`) the cropland area (`lu_area(lucrop,jpngr)`). Note the difference: The prescribed cropland area (`lu_area(lucrop,jpngr)`) is the area *currently* (in a given year) under cultivation and fallow areas don't count toward that value. This implies that an additional 50% is fallow at each given point in time. The land turnover rate (`ltor`) is `fallow_factor / 3.d0`. Hence, for pre-1850 years, 50% of the land is "abandoned", i.e. converted from actually cultivated cropland to fallow, which is treated as secondary land in LPX.  

```Fortran
!     -------------------------------------------------------------------------
!     CROPSUIT and LAND TURNOVER RATE
!     Land fraction suitable from cropland agriculture. 
!     -------------------------------------------------------------------------
          fallow_factor = max(
     $         1.0d0,
     $         min(
     $         3.0d0/2.0d0,
     $         3.0d0/2.0d0 - 0.5d0/110.0d0 * (realyear-1850.0d0)
     $         ) )
          
          if (shifting_cultivation(jpngr)) then
            cropsuit(jpngr) = accessible(jpngr)
            ltor(jpngr)     = 0.25d0
          else
            cropsuit(jpngr) = max( lu_area(lucrop,jpngr),
     $           min(
     $           accessible(jpngr),
     $           fallow_factor*lu_area(lucrop,jpngr)
     $           ))
            ltor(jpngr)    = fallow_factor / 3.d0
          endif

```

3. **Only relevant for model developers** The land use transition matrix is constructed. This contains $i \times j$ values, where $i$ and $j$ are the land use categories (primary, secondary, cropland, pasture), and the martrix $M_{i,j}$ (variable `DF_tr` below) defines the gridcell area fraction transiting from land use category $i$ to $j$. 

```Fortran
!     ADD LAND TURNOVER (shifting cultivation)
!     -------------------------------------------------------------------------
          
!     CLAIM CROPLAND/PASTURE
!     only cropland turnover, priority for primary land,
!     accessible primary land constrained.
          lu=lucrop
          
!     first priority, claim from lunat
          req=min(
     $         lu_area_tr(lu,jpngr)*ltor(jpngr),
     $         luold(lu)*ltor(jpngr)                     !required for conversion to crop
     $         )
          avl=max(
     $         max( 0.d0, luold(lunat)-inaccess(jpngr)),
     $         0.d0
     $         )
          con=min(req,avl)                                  !actually converted primary land
          DF_tr(lunat,lu,jpngr)=DF_tr(lunat,lu,jpngr)+con
          req=req-con
          luold(lunat)=luold(lunat)-con
          lu_area_tr(lunat,jpngr)=lu_area_tr(lunat,jpngr)-con
          lu_area_tr(lu,jpngr)=lu_area_tr(lu,jpngr)+con
          if (req.gt.0.0d0) then
!     second priority, claim from lusecd
            avl=luold(lusecd)
            con=min(req,avl)
            DF_tr(lusecd,lu,jpngr)=DF_tr(lusecd,lu,jpngr)+con
            req=req-con
            luold(lusecd)=luold(lusecd)-con
            lu_area_tr(lusecd,jpngr)=lu_area_tr(lusecd,jpngr)-con
            lu_area_tr(lu,jpngr)=lu_area_tr(lu,jpngr)+con
            if (req.gt.0.0d0) then
!     not enough primary and secondary land available to satisfy cropland/pasture abandonment
              uns=req                                       !un-satisfied requirement
            endif
          endif
            
            
!     ABANDON CROPLAND (limited by available primary and secondary)
!     crop/pasture -> secondary (all goes into secondary)
          lu=lucrop
          DF_tr(lu,lusecd,jpngr)=sum(DF_tr(:,lu,jpngr))
          luold(lu)=luold(lu)-DF_tr(lu,lusecd,jpngr)
          lu_area_tr(lu,jpngr)=lu_area_tr(lu,jpngr)-DF_tr(lu,lusecd,jpngr)
          lu_area_tr(lusecd,jpngr)=lu_area_tr(lusecd,jpngr)+DF_tr(lu,lusecd,jpngr)
          
        endif       
```

4. **Only relevant for model developers** Add harvested area to the transition matrix. To be completed.

